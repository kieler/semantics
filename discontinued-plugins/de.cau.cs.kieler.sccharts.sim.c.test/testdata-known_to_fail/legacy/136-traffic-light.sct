scchart TRAFFIC_LIGHT {
  input signal pure Error;
  input signal pure Ok;
  input signal pure Sec;
  signal pure Pgo;
  signal pure Pstop;
  output signal pure PG;
  output signal pure PR;
  output signal pure CG;
  output signal pure CR;
  output signal pure CY;

  region _R0:

  initial state Normal {

    region Pedestrian:

    initial state Pred {
      during / PR;
    }
    --> Pgreen with Pgo;

    state Pgreen {
      during / PG;
    }
    --> Pred with Pstop;

    region Car:

    state Cgreen {
      during / CG;
    }
    --> Cyellow1 with 5 Sec / Pgo;

    state Cyellow1 "Cyellow" {
      during / CY;
    }
    --> Cred2 with Sec;

    initial state Cred2 "Cred" {
      during / CR;
    }
    --> Cyellow with 3 Sec;

    state Cyellow {
      during / CY;
    }
    --> Cgreen with Sec / Pstop;
  }
  o-> Error with Error;

  state Error {

    region Pedestrian:

    initial state Pred {
      during / PR;
    };

    region Car:

    state Cyellow_on {
      during / CY;
    }
    --> Cyellow_off with Sec;

    initial state Cyellow_off
    --> Cyellow_on with Sec;
  }
  o-> Normal with Ok;
}