scchart MedicalAid {
  input bool intensive;
  input bool cardio;
  input bool attack;
  input bool infection;
  input bool trauma;
  output bool bMDone;
  output bool gMDone;
  output bool tMDone;
  output bool cMDone;
  
  region Main:
  initial state Init
  --> Monitoring immediate with / bMDone = false; gMDone = false; 
  tMDone = false; cMDone = false; 'reportInit()';
  
  state Monitoring{
    region HandleMonitoring:
    
     initial state Start
     --> Severity immediate;
     
     state Severity
     --> Intensive immediate with intensive / 'setNotification()'
     --> Normal immediate with / 'initiatePatientFile()';
    
     state Intensive{
      region IntensiveModes:
      initial state Start
      --> ModeChoice immediate;
      state ModeChoice
      --> Cardiovascular immediate with cardio / 'alertCardio()'
      --> Trauma immediate with trauma / 'alertSurgery()'
      --> General immediate with / 'alertGeneral()';
      state Cardiovascular{
        region Cardio:
        initial state Start
        --> OP immediate;
        state OP
        --> Modechange immediate with attack / 'alertSurgery()'
        --> CardioMonitoring immediate;
        state CardioMonitoring{
          region PatientValues:
          initial state Start
          --> Check1 immediate with / 'checkBloodPressure()';
          state Check1
          --> Check2 immediate with / 'checkBloodSugar()';
          state Check2
          --> Done immediate with / 'checkHeartRate()';
          final state Done;
          region Medication:
          initial state Start
          --> Medication immediate with / 'calculateDose()';
          state Medication
          --> Done immediate with / 'writeLog()';
          final state Done; 
        };
        final state Modechange;
      }
      >-> SwitchMode;
      state SwitchMode
      --> Trauma immediate with trauma / 'reportModeChange()'
      --> Done immediate;
      state Trauma{
        region CirculationChecks:
          initial state Start
          --> Check1 immediate with / 'checkBloodPressure()';
          state Check1
          --> Done immediate with / 'checkHeartRate()';
          final state Done;
        region AnestesiaChecks:  
          initial state Start
          --> Check1 immediate with / 'checkOxigen()';
          state Check1
          --> Done immediate with / 'checkMuscleTone()';
          final state Done;
        region Display:
          initial state Start
          --> Done immediate with / 'display()';
          final state Done;
      };
      state General{
        region GeneralCases:
        initial state Start
        --> Check1 immediate with / 'checkBloodPressure()';
        state Check1
        --> InfectionChecks immediate with infection
        --> TriggerManualChecks immediate;
        state InfectionChecks {
          region HandleInfectionChecks:
          initial state Start
          --> Check1 immediate with / 'checkTemperature()';
          state Check1
          --> Done immediate with / 'checkHydration()';
          final state Done;
        }
        >-> Check2 immediate;
        state TriggerManualChecks{
          region HandleTriggerManualChecks:
          initial state Start
          --> Done immediate with / 'displayManualCheckList()';
          final state Done;
        }
        >-> Check2 immediate;
        state Check2
        --> Done immediate with /'writeLog()';
        final state Done;
      };
      final state Done;
     };
     
     state Normal{
      region Basic:
      initial state Start
      --> Check1 immediate with / 'checkBloodPressure()';
      state Check1
      --> Check2 immediate with / 'checkHeartRate()';
      state Check2
      --> Done immediate with / 'checkTemperature()';
      final state Done;
    } >-> Done immediate;
    final state Done;
  }
  >-> MonitoringDone;
  state MonitoringDone
  --> Init;
  }
  



