// The TRAFFIC LIGHT example defines a Normal and an Error state. Each have two 
// concurrent threads, one for the Pedestrian and another one for the Car. In the 
// Normal state, the Car thread circles between Cgreen, Cred, and two Cyellow 
// states. It sends a Pstop signal to the Pedestrian thread when transitioning to 
// Cgreen. It sends a Pgo signal to the Pedestrian thread when transitioning to
// Cred. The Pedestrian thread circles between two states Pred and Pgreen depending
// on the Car threads signals. In the Error mode, the Pedestrian light is perma-
// nently set to red and the Car light blinks yellow. 
//
// The example is taken from the following publication:
// Reinhard von Hanxleden, Edward A. Lee, Christian Motika, and Hauke Fuhrmann. 
// Multi-view modeling and pragmatics in 2020 --- Position paper on designing 
// complex cyberphysical systems. Monterey Workshop. LNCS. 2012.

scchart TRAFFIC_LIGHT {
  input signal Error;
  input signal Ok;
  input signal Sec;
  signal Pgo;
  signal Pstop;
  output signal PG;
  output signal PR;
  output signal CG;
  output signal CR;
  output signal CY;

  region _R0:

  initial state Normal {

    region Pedestrian:

    initial state Pred
    --> Pgreen with Pgo;

    state Pgreen
    --> Pred with Pstop;

    region Car:

    state Cgreen
    --> Cyellow1 with 5 Sec / Pgo;

    state Cyellow1 "Cyellow"
    --> Cred2 with Sec;

    initial state Cred2 "Cred"
    --> Cyellow with 3 Sec;

    state Cyellow
    --> Cgreen with Sec / Pstop;
  }
  o-> Error with Error;

  state Error {

    region Pedestrian:

    initial state Pred;

    region Car:

    state Cyellow_on
    --> Cyellow_off with Sec;

    initial state Cyellow_off
    --> Cyellow_on with Sec;
  }
  o-> Normal with Ok;
}