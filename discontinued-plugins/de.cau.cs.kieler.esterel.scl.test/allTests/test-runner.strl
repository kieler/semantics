module RUNNER :

input METER;
input SECOND;
input MORNING;
input LAP;
input STEP;
input HEART_BEAT;

output WALK;
output RUN;
output JUMP;
output GO_TO_WORK;
output GO_TO_HOSPITAL;

relation SECOND # METER # STEP;
relation MORNING => SECOND;
relation LAP => METER;

trap HEART_ATTACK in
   every MORNING do
      do
         loop
            do
               emit WALK
            upto 100 METER;
            signal HEART_ATTACK in
               do
                  do
                     every STEP do emit JUMP end
                  upto 15 SECOND;
                  emit RUN;
               watching HEART_ATTACK timeout exit HEART_ATTACK end
            ||
               copymodule CHECK_HEART
            end
         each LAP
      upto 2 LAP;
      emit GO_TO_WORK
   end
handle HEART_ATTACK do
   emit GO_TO_HOSPITAL
end
.

module CHECK_HEART :
input SECOND;
input HEART_BEAT;
output HEART_ATTACK;

loop
   await SECOND do emit HEART_ATTACK end
each HEART_BEAT
.

