grammar de.cau.cs.kieler.prom.KiBuild with de.cau.cs.kieler.kexpressions.KExpressions

generate kibuild "http://www.cau.de/cs/kieler/prom/KiBuild"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://kieler.cs.cau.de/kexpressions/0.1.2" as kexpressions 

BuildConfiguration:
	(attributes+=AttributeMapping)*
	(modelCompilers+=ModelCompiler)*
	(simulationCompilers+=SimulationCompiler)*
	(templateProcessors+=TemplateProcessor)*
;

ModelCompiler:
	'model' 'compiler' name=ID '{'
        (attributes+=AttributeMapping)*
        (simulationProcessor=SimulationTemplateProcessor)?
	'}'
;

SimulationCompiler:
    'simulation' 'compiler' name=ID '{'
        (attributes+=AttributeMapping)*
    '}'
;

TemplateProcessor:
    NormalTemplateProcessor
    | SimulationTemplateProcessor
    | WrapperCodeTemplateProcessor
;

NormalTemplateProcessor returns TemplateProcessor:
    'process' 'template' '{'
        {NormalTemplateProcessor}
        (attributes+=AttributeMapping)*    
    '}'
;

SimulationTemplateProcessor returns TemplateProcessor:
    'process' 'simulation' 'template' '{'
        {SimulationTemplateProcessor}
        (attributes+=AttributeMapping)*    
    '}'
;

WrapperCodeTemplateProcessor returns TemplateProcessor:
    'process' 'wrapper' 'template' '{'
        {WrapperCodeTemplateProcessor}
        (attributes+=AttributeMapping)*    
    '}'
;

AttributeMapping:
    name=ID
    (arrayIndex = ArrayIndex)?
    ((':' value=Literal)
    | (':' values+=Literal (',' values+=Literal)+)
    | (':'? '{' (attributes+=AttributeMapping)+ '}'))
;

Literal:
    (value=TextValue (arrayIndex = ArrayIndex)?)
   | value = SignedInt
   | value = SignedFloat
   | value = AnyValue
;

ArrayIndex:
    ('[' indices+=INT ']')+
;

enum Sign:
    POSITIVE='+' | NEGATIVE='-'
;

SignedFloat:
    (sign = Sign)?
    value = FLOAT
;

SignedInt:
    (sign = Sign)?
    value = INT
;

TextValue:
    value=ID
;

// Overwrite BOOLEAN and ID to have a less strict definition of IDs
terminal BOOLEAN returns ecore::EBooleanObject :
    'true' | 'false';

// IDs may contain letters, dots, slashes, dashes, underscores and numbers as well as brackets.
// Thus it is possible have a path (e.g. '/abc/m3.x') written as ID instead of a string 
terminal ID returns ecore::EString:
    '^'?('a'..'z'|'A'..'Z'|'_'|'/'|'\\') ('a'..'z'|'A'..'Z'|'_'|'-'|'.'|'/'|'\\'|'0'..'9')*
;