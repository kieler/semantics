@Transformation ABORT
scchart AbortsAndRegions {
   
  // We use these variables to have different triggers
  // and actions for each transition.
  input int I;
  output int O;
  
   initial state M{
     region A:
       initial state A1
       --> A2;
       
       state A2
       --> A3 immediate;
       
       final state A3;
        
     region B:
       initial state B1
       --> B2 immediate;
       
       state B2
       --> B3;
       
       final state B3;
   }
   o-> S1 with I == 1 / O = 1
   o-> S2 immediate with I == 2 / O = 2
   --> W1 with I == 3 / O = 3
   --> W2 immediate with I == 4 / O = 4
   >-> T1 with I == 5 / O = 5
   >-> T2 immediate with I == 6 / O = 6;
   
   // S for strong abort
   state S1;
   state S2;
   // W for weak abort
   state W1;
   state W2;
   // T for termination
   state T1;
   state T2;
}