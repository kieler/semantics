//grammar de.cau.cs.kieler.s.S with org.eclipse.xtext.common.Terminals
//		 de.cau.cs.kieler.core.expressions.Expressions, 
//		 de.cau.cs.kieler.core.annotations.Annotations
grammar de.cau.cs.kieler.s.S with de.cau.cs.kieler.core.kexpressions.KExpressions
//grammar de.cau.cs.kieler.s.S with de.cau.cs.kieler.synccharts.text.actions.Actions
//grammar de.cau.cs.kieler.s.S with de.cau.cs.kieler.synccharts.text.interfaces.Interfaces

//import 'http://kieler.cs.cau.de/expressions' as expressions --> wrong
//import 'http://kieler.cs.cau.de/annotations' as annotations

import "http://kieler.cs.cau.de/kexpressions/0.1.2" as kexpressions
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
//import "platform:/resource/de.cau.cs.kieler.core.annotations/model/annotations.ecore" as annotations

generate s "http://www.cau.de/cs/kieler/s"


Program :
	'synchronous program' name = ID '(' priority=INT ')'
//	((signals += Signal) (signals += Signal)*)?
//	(((signals+=Signal)+ ';') | ((variables+=Variable)+ ';'))? 
//	(((variables+=Variable)|(signals += Signal)) ((variables+=Variable)|(signals += Signal))*)?
    (((valuedObjects+=ValuedObject))*)?
	(globalHostCodeInstruction = HOSTCODE)*
	(states += State)+
;



//Annotation:
//	'@' key = ID ':'
//	value = STRING
//;

//TODO: Consolidate with Annotatable xtext grammar somehow... (s.a.)
//Annotatable returns annotations::Annotatable:
// 	State | Instruction;


State:
	'state' '(' name = ID ')' '{'
	(((valuedObjects+=ValuedObject)+ ';'))? 
	(instructions+=Instruction';')*
	'}'
;

// TODO: ensure signal AND variable declaration in top level....


Thread:
	'thread' '(' name = ID ')' '{'
	(((valuedObjects+=ValuedObject)+ ';'))? 
	(states+=State';')*
	'}'
;


ValuedObject returns kexpressions::ValuedObject:
    (isInput?='input')?
	(isOutput?='output')?
	(isSignal?='signal')?
	(name=EString)
	(':=' initialValue=EString)?
    ((':' type=ValueType)
      |
     (':' 'combine' type=ValueType 'with' (combineOperator=CombineOperator) )
    )? ";";
    

//---------------------------------------------------------------------
//----------------------- List of S-Instructions ----------------------
//---------------------------------------------------------------------

HostCodeInstruction:
	hostCode = HOSTCODE
;

// custom terminal rule for strings
terminal HOSTCODESTRINGTERMINAL returns ecore::EString:
    '$' ('a'..'z'|'A'..'Z'|'_'|'.'|'"'|'0'..'9')+ '$';
    

Continuation:
	 State | Thread
;

Instruction:
   Halt | Abort | Join | Pause | Term | If | Trans | Fork | LocalSignal | Emit | Await | Prio | HostCodeInstruction
;

// Change priority
Prio:
	'prio' '('priority=INT (',' continuation=[Continuation])?')'
;

// Transition to anothe Thread or State   
Trans :
	'trans''(' continuation=[Continuation] ')'
;
  
// Forking new Threads
Fork :
	'fork''('thread=[Continuation] ',' priority=INT (',' continuation=[Continuation])?')'
;


// Joining Threads
Join : 
	{Join}
	'join' '('continuation=[Continuation]?')'
;
   

// Simple instructions
Pause :
	{Pause} 
	'pause''('continuation=[Continuation]?')'
;
  
Term : 
	{Term}
	'term''('continuation=[Continuation]?')'
;
  
Halt : 
	{Halt}
	'halt''('continuation=[Continuation]?')'
;
  
  
LocalSignal: // For handling local signal re-initialization (taking care or reincarnation)
    'signal''('signal=[kexpressions::ValuedObject]')'
;

Emit:
	'emit''('signal= [kexpressions::ValuedObject] ('(' value=SExpression ')')? (',' continuation=[Continuation])?')'
;
  
Abort : 
	{Abort}
	'abort''('continuation=[Continuation]?')'
;

// Conditioned execution of instructions  
If : 
	'if''('expression = SExpression (',' continuation=[Continuation])?')' '{'
		(instructions += Instruction';')*
	'}'
;
  
// Complex instruction
Await:
	'await' '('signal= [kexpressions::ValuedObject] (',' continuation=[Continuation])?')'
;


SExpression returns kexpressions::Expression:
	Expression | HOSTCODE
;
