grammar de.cau.cs.kieler.scl.SCL with org.yakindu.sct.model.stext.SText 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.yakindu.org/sct/sgraph/2.0.0" as sgraph
import "http://www.yakindu.org/base/types/2.0.0" as types
import "http://www.yakindu.org/base/base/2.0.0" as base
import "http://www.yakindu.org/sct/statechart/SText" as stext
import "http://kieler.cs.cau.de/annotations" as annotations

generate scl "http://www.cau.de/cs/kieler/scl/scl"

Program:
	'module' name = ID
	(declarations+=VariableDeclaration)*
	'{'
        ((
            ((statements += InstructionStatement';') | statements += EmptyStatement)*
           statements +=  InstructionStatement
           statements +=  EmptyStatement*
         )|
         statements +=  EmptyStatement*)
    '}' 
;

//KeyStringValueAnnotation returns annotations::StringAnnotation:
//    '@' name=ID value=STRING
//;

VariableDeclaration:
    (input?='input')? (output?='output')? 'signal' (name=ID)(':'type=[types::Type])?('='initialValue=Expression)?';'
;

Statement:
    EmptyStatement | InstructionStatement
;

EmptyStatement:
    (annotation += SL_COMMENT)*
    (label = ID ':')
;

InstructionStatement:    
    (
        (annotation += SL_COMMENT)*
//        (label = ID ':')?
        instruction = (Assignment | Conditional | Goto | Parallel | Pause | StatementScope)
    )
;

Instruction:
    Assignment | Conditional | Goto | Parallel | Pause | StatementScope
;

Assignment:
    assignment = Expression
;

Conditional:
    'if' expression = Expression 'then'
        ((
            ((statements += InstructionStatement';') | statements += EmptyStatement)*
           statements +=  InstructionStatement
           statements +=  EmptyStatement*
         )|
         statements +=  EmptyStatement*)
    'end' 
;

Goto:
    'goto' targetLabel = ID 
;

//AbstractThread:
//    Thread | Program
//;

StatementSequence:
    Thread | Program | Conditional | StatementScope
;

Thread:
    {Thread}
        ((
            ((statements += InstructionStatement';') | statements += EmptyStatement)*
           statements +=  InstructionStatement
           statements +=  EmptyStatement*
         )|
         statements +=  EmptyStatement*)
;

Parallel:
    'fork'
    (    threads += Thread
    ('par'
         threads += Thread)*)
    'join'
;


Pause:
    'pause' {Pause}
;

StatementScope:
    {StatementScope}
    '{'
    (declarations+=VariableDeclaration)*
        ((
            ((statements += InstructionStatement';') | statements += EmptyStatement)*
           statements +=  InstructionStatement
           statements +=  EmptyStatement*
         )|
         statements +=  EmptyStatement*)
    '}'    
;
