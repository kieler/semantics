/*
 * KIELER - Kiel Integrated Environment for Layout Eclipse RichClient
 *
 * http://www.informatik.uni-kiel.de/rtsys/kieler/
 * 
 * Copyright 2010 by
 * + Christian-Albrechts-University of Kiel
 *   + Department of Computer Science
 *     + Real-Time and Embedded Systems Group
 * 
 * This code is provided under the terms of the Eclipse Public License (EPL).
 * See the file epl-v10.html for the license text.
 * 
 *****************************************************************************/
grammar de.cau.cs.kieler.synccharts.interfacedeclparser.InterfaceDecl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "platform:/resource/de.cau.cs.kieler.synccharts/model/synccharts.ecore" as synccharts

generate interfaceDecl "http://www.cau.de/cs/kieler/synccharts/interfacedeclparser/InterfaceDecl"

//==============================================================================
// "Buckets" containing the Signals / Regions with Signals
//==============================================================================

StateExtend:
	(
	("input" (inputSignals+=InputSignals)) |
	
	("output" (outputSignals+=OutputSignals)) |
	
	("input" "output" (inOutputSignals+=InOutputSignals))  |
	
	("signal" (signals+=Signals)) |	

	(regions+=RegionSignalDec)
	)+
	;

RegionSignalDec:
	region = [synccharts::Region|ID] ":" (
	("var" vars+=Variable ("," (vars+=Variable))*) |
	("signal" signals+=Signal ("," (signals+=Signal))*) 	
	)
	(
	("," "var" vars+=Variable ("," (vars+=Variable))*) |
	("," "signal" signals+=Signal ("," (signals+=Signal))*) 	
	)* 
	";"
	;

Signals:
	(signals+=Signal ("," (signals+=Signal))*) ";"
	;
	
InputSignals:
	(signals+=Signal ("," (signals+=Signal))*) ";"
	;

OutputSignals: 
	(signals+=Signal ("," (signals+=Signal))*) ";"
	;

InOutputSignals:
	(signals+=Signal ("," (signals+=Signal))*) ";"
	;

//==============================================================================
// synccharts elements as needed
//==============================================================================

Signal returns synccharts::Signal:
 
	(
	name=ID |
	name=ID (":=" initialValue=STRING) |
	name=ID (":=" initialValue=STRING) (type=ValueType) |
	name=ID (":=" initialValue=STRING)  ("combine" (type=ValueType) "with" (combineOperator=CombineOperator)) |
	name=ID (":=" initialValue=STRING) (hostType=STRING) |
	name=ID (":=" initialValue=STRING)  ("combine" (hostType=STRING) "with" (hostCombineOperator=STRING)) |
	name=ID ("combine" (type=ValueType) "with" (combineOperator=CombineOperator)) |
	name=ID ("combine" (hostType=STRING) "with" (hostCombineOperator=STRING))
	)
	;
		
	
Variable returns synccharts::Variable:
	name=ID
	(":=" initialValue=STRING)?
	(type=ValueType)?
	("host" hostType=STRING)?
;
		
enum ValueType returns synccharts::ValueType:
	PURE="PURE" | BOOL="BOOL" | UNSIGNED="UNSIGNED"	| 
	INTEGER="INT" | FLOAT="FLOAT" | HOST="HOST"	
;

enum CombineOperator returns synccharts::CombineOperator:
	NONE="NONE" | ADD="+" | MULT="*" | MAX="max" |
	MIN="min" | OR="or" | AND="and" | HOST="host"
;
