/*
 * KIELER - Kiel Integrated Environment for Layout Eclipse RichClient
 * 
 * http://www.informatik.uni-kiel.de/rtsys/kieler/
 * 
 * Copyright 2023 by
 * + Kiel University
 *   + Department of Computer Science
 *     + Real-Time and Embedded Systems Group
 * 
 * This code is provided under the terms of the Eclipse Public License (EPL).
 * See the file epl-v10.html for the license text.
 */
grammar de.cau.cs.kieler.verification.ltl.LTLFormula with de.cau.cs.kieler.kexpressions.kext.KExt

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://kieler.cs.cau.de/kexpressions/0.1.2" as kexpressions
import "http://kieler.cs.cau.de/keffects/0.1.0" as keffects
import "http://kieler.cs.cau.de/kext/0.1.0" as kext

generate lTLFormula "http://www.cau.de/cs/kieler/verification/ltl/LTLFormula"

LTLFormula returns LTLExpression:
    expr = LTLExpression
    declarations+=Declaration*
    variableNames += STRING*;

LTLExpression returns kexpressions::Expression:
    {LTLExpression} operator=(GlobalOperator | FinalOperator | NextOperator) '(' subExpressions+=LTLExpression ')'
    | BoolExpression 
    ({OperatorExpression.subExpressions+=current} 
        (operator=UntilOperator subExpressions+=BoolExpression)
        ('U' '(' subExpressions+=BoolExpression ')')*
    )?;

enum GlobalOperator returns LTLOperatorType:
    GLOBAL="G";

enum FinalOperator returns LTLOperatorType:
    FINAL="F";

enum NextOperator returns LTLOperatorType:
    NEXT="X";

enum UntilOperator returns LTLOperatorType:
    UNTIL="U";
    
@Override
AtomicExpression returns kexpressions::Expression:
    BoolValue
    | '(' LTLExpression ')'
    | ReferenceCall
    | FunctionCall
    | RandomCall
    | RandomizeCall
    | ValuedObjectTestExpression // Last to allow detection of calls
    | SpecialAccessExpression
    | TextExpression;
	
	