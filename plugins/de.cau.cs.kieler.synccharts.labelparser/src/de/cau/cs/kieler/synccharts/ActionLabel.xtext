grammar de.cau.cs.kieler.synccharts.TransitionLabel with org.eclipse.xtext.common.Terminals

generate transitionlabel "http://kieler.cs.cau.de/synccharts/actionlabel"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://kieler.cs.cau.de/synccharts" as synccharts
//import "platform:/resource/de.cau.cs.kieler.synccharts/model/synccharts.ecore" as synccharts
//import "synccharts" as synchcharts

//==============================================================================
// Basic Actions
//==============================================================================

Action returns synccharts::Action:
	(isImmediate?='#')? (delay=INT)? (trigger=BooleanExpression)? ("/" (effects+=Effect (',')? )*)?; 		

Effect returns synccharts::Effect: 
	Emission | Assignment | HostCode;
		
Emission returns synccharts::Emission:
	signal=[synccharts::Signal] ("(" newValue=ValueExpression ")")?;

Assignment returns synccharts::Assignment:
	variable=[synccharts::Variable] ":=" expression=ValueExpression;

SignalReference returns synccharts::SignalReference:
	signal = [synccharts::Signal];

VariableReference returns synccharts::VariableReference:
	variable = [synccharts::Variable];

Signal returns synccharts::Signal:
	name=ID;
	
Variable returns synccharts::Variable:
	name=ID;

//==============================================================================
// Values
//==============================================================================

IntValue: 
	value=INT;
FloatValue:
    value=Float;
BooleanValue:
    value=Boolean;
Value:
	IntValue | FloatValue | BooleanValue;

// make sure the Float rule does not shadow the built-in INT rule
terminal Float returns ecore::EDouble : ((INT"."INT | INT("."INT)?("e"|"E")("-"|"+")?INT))"f"? | INT"f";
terminal Boolean returns ecore::EBoolean : "true" | "false";

//==============================================================================
// Expressions
//==============================================================================

// Example: ?A	
ValOperation returns synccharts::ComplexExpression:
	operator=ValOperator subExpressions+=SignalReference;

// Example: 42, ?A, var1
ValueExpression returns synccharts::Expression:
	IntValue | FloatValue | ValOperation | VariableReference;

// Example: 42 <= ?A
CompareOperation returns synccharts::Expression:
	ValueExpression ({Operation.subExpressions+=current} operator=CompareOperator subExpressions+=ValueExpression);  	

// Example: not A, not false, not (A or B)
// at the latter we need the parans to indicate the right binding
NotOperation returns synccharts::ComplexExpression:
	operator=NotOperator subExpressions+=ParanthesedBooleanExpression;

// Example: either negated or normal expression (with parantheses) 
NotOrNormalExpression returns synccharts::Expression:
	NotOperation | ParanthesedBooleanExpression;

// Example: A and B, not C and 42 <= ?D
AndOperation returns synccharts::Expression:
	NotOrNormalExpression ({Operation.subExpressions+=current} operator=OperatorAnd subExpressions+=NotOrNormalExpression)*;

// Example: A or B, C and B or D and not E 
OrOperation returns synccharts::Expression:
	AndOperation ({Operation.subExpressions+=current} operator=OperatorOr subExpressions+=AndOperation)*;

// Example: true, A, 42>var1, (A or B), (not D and C or ?E = 42)
ParanthesedBooleanExpression returns synccharts::Expression:
    BooleanValue | SignalReference | CompareOperation | '('OrOperation')' ;

// Example: not D and C or ?E = 42 or not (A and (B or C))
BooleanExpression returns synccharts::Expression:
	OrOperation ;

//==============================================================================
// Operators
//==============================================================================
	
enum OperatorType returns synccharts::OperatorType:
	EQ="=" | LT="<" | LEQ="<=" | GT=">" | GEQ=">=" | NOT="not"| AND="and" | OR="or" | ADD="+" | SUB="-" | MULT="*" | DIV="div" | MOD="mod" | VAL="?" | PRE="pre";

enum ValOperator returns synccharts::OperatorType:
	VAL="?";

enum CompareOperator returns synccharts::OperatorType:
	EQ="=" | LT="<" | LEQ="<=" | GT=">" | GEQ=">=";
	
enum NotOperator returns synccharts::OperatorType:
	NOT="not";

enum OperatorOr returns synccharts::OperatorType:
	OR="or";

enum OperatorAnd returns synccharts::OperatorType:
	AND="and";

//==============================================================================
// Misc
//==============================================================================
	
HostCode returns synccharts::HostCode:
	//"'"code=STRING"'";
	code=STRING ("(" type=ID ")")?; 	
	