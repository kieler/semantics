import uml;
import ecore;
//import "http://www.eclipse.org/emf/2002/Ecore";

extension org::eclipse::xtend::util::stdlib::io;          // debug printouts
extension org::eclipse::xtend::util::stdlib::crossref;    // find elements that reference others
extension org::eclipse::xtend::util::stdlib::cloning;     // copy model elements and whole trees
extension org::eclipse::xtend::util::stdlib::tracing;     // create map between transformed objects
extension org::eclipse::xtend::util::stdlib::issues;      // pass messages back to java (e.g. to UI)

//-----------------------------------------------------------------------------

// This method will immitate a transition trigger and return its String representation
String trigger2(Transition transition):
  let transitionLabel = transition.name:
  let returnTrigger = getTrigger(transitionLabel.toString()):
  //TODO remove the noevents introduced by initial transitions
  if (returnTrigger != null && returnTrigger.length > 0) then
    returnTrigger
  else
    '(ev: "noevent")'
;


// This method will immitate a transition gurad and return its String representation
String guard2(Transition transition):
  let transitionLabel = transition.name:
  let returnGuard = getGuard(transitionLabel.toString(), transition):
  if (returnGuard != null && returnGuard.length > 0) then
    returnGuard
  else
    'true'
;

// This method will immitate a transition action and return its String representation
String action2(Transition transition):
  let transitionLabel = transition.name:
  let returnAction = getAction(transitionLabel.toString()):
  if (returnAction != null && returnAction.length > 0) then
    returnAction
  else
    'skip'
;

// INTERNAL: Helpermethod for action extraction
String getAction(String transitionLabel):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getAction(java.lang.String)
;

// INTERNAL: Helpermethod for trigger extraction
String getTrigger(String transitionLabel):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getTrigger(java.lang.String)
;

// INTERNAL: Helpermethod for guard extraction
String getGuard(String transitionLabel, Transition transition):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getGuard(java.lang.String, org.eclipse.uml2.uml.Transition)
;


// Get all trigger events that are used in the statemachine.
Set[String] getTriggerEvents(Package package):
 let transitions = package.getTransitions() :
 transitions.select(e|e.name != null && !(e.source.isInitial())).trigger2()
;
  //TODO remove the noevents introduced by initial transitions


// Get all trigger events that are used in the statemachine.
Set[String] getTriggerEvents(StateMachine sm):
 let transitions = sm.getTransitions() :
 transitions.select(e|e.name != null).trigger2()
;

// INTERNAL: Helpermethod for action extraction
Set[String] getActions(Package package):
 let transitions = package.getTransitions() :
 transitions.select(e|e.name.toString().contains("/")).action2()
;

// INTERNAL: Helpermethod for action extraction
Set[String] getActions(StateMachine sm):
 let transitions = sm.getTransitions() :
 transitions.select(e|e.name.toString().contains("/")).action2()
;

//-----------------------------------------------------------------------------

// Returns the EMF id for a vertex maude state id.
// Ideally this should return just one vertex, but cause must be paied because
// the maude ide is constructed using just the hash value. In theory this may
// lead to duplicate maude state ids.
Set[String] getEmfId(Package package, String maudeStateRegionId) :
 let vertexList = (Set[Vertex])package.getStatesRegions():
 let vertexSelectedList = vertexList.select(e|e.getId().toString().matches(maudeStateRegionId.toString())):
 vertexSelectedList.getEMFId()
;
Set[String] getEmfId(StateMachine sm, String maudeStateRegionId) :
 let vertexList = (Set[Vertex])sm.getStatesRegions():
 let vertexSelectedList = vertexList.select(e|e.getId().toString().matches(maudeStateRegionId.toString())):
 vertexSelectedList.getEMFId()
;

//-----------------------------------------------------------------------------

// Tests whether this initial PseudoState vertex has a parent containing state that is
//   1. the top-level state machine OR 
//   2. is marked DIRECTLY by an initial state (or a chain of beginning with a top-level state machine).
boolean hasInitialParent(Vertex vertex):
	let isStateMachine = vertex.eContainer.eContainer.metaType == StateMachine:
	let isInitialContainer = ((vertex.eContainer.eContainer.metaType == State) &&
	                          (getInitialPseudostatesPointingTo((State)vertex.eContainer.eContainer).size > 0)
	                         ):
	switch {
	  // the easy case
	  case isStateMachine : true
	  // validate recursiveley
	  case isInitialContainer : hasInitialParent(getInitialPseudostatesPointingTo((State)vertex.eContainer.eContainer).first())
	  // if nothing from above is true we can give up 
	  default : false
	}
;


// Get the list of InitialPseudo vertices pointing to a state 
List[Vertex] getInitialPseudostatesPointingTo(State state):
	let initialPseudoStates = (Set[Vertex])state.eRootContainer.eAllContents.select(e|e.metaType == Pseudostate && ((Vertex)e).isInitial()):
	// target gets a list of targets, the state we are searching for must appear in that list
	initialPseudoStates.flatten().select(e|e.outgoing.target.contains(state))
;


// Get ids of all initial state targets and their regions
Set[String] getInitialStates(Package package):
	let allInitialPseudoStates = (Set[Vertex])package.eAllContents.select(e|e.metaType == Pseudostate && ((Vertex)e).isInitial()):
	let initialPseudoStates = allInitialPseudoStates.select(e|e.hasInitialParent()):
	let initialStates = initialPseudoStates.outgoing.target:
	let initialRegions = initialStates.getParent():
	let all = initialStates.addAll(initialRegions):
	all.getId()
;

// Get ids of all initial state targets and their regions
Set[String] getInitialStates(StateMachine sm):
	let allInitialPseudoStates = (Set[Vertex])sm.eAllContents.select(e|e.metaType == Pseudostate && ((Vertex)e).isInitial()):
	let initialPseudoStates = allInitialPseudoStates.select(e|e.hasInitialParent()):
	let initialStates = initialPseudoStates.outgoing.target:
	let initialRegions = initialStates.getParent():
	let all = initialStates.addAll(initialRegions):
	all.getId()
;

//-----------------------------------------------------------------------------

// Get all transition of the state machine
Set[Transition] getTransitions(StateMachine sm):
 ((Set[Transition])sm.eAllContents.select(e|e.metaType == Transition))
;

// Get all transition of an general Package
Set[Transition] getTransitions(Package package):
 ((Set[Transition])package.eAllContents.select(e|e.metaType == Transition))
;

// Get all Fork or Join or Choice vertices of the state machine (these are Pseudostates)
Set[Pseudostate] getForkJoinChoice(StateMachine sm):
 ((Set[Transition])sm.eAllContents.select(e|e.metaType == Pseudostate && ( ((Vertex)e).isJoin() || ((Vertex)e).isFork() || ((Vertex)e).isChoice() ) ))
;


// Get all States and Regions of the state machine
Set[Vertex] getStatesRegions(Package package):
 //ToDo: FinalState is State ?!?
 ((Set[Vertex])package.eRootContainer.eAllContents.select(e|e.metaType == State || e.metaType == FinalState || e.metaType == Region))
;

// Get all States of the state machine
Set[Vertex] getStates(Package package):
 //ToDo: FinalState is State ?!?
 ((Set[Vertex])package.eRootContainer.eAllContents.select(e|e.metaType == State || e.metaType == FinalState))
;

// Get all States and Regions of the state machine
Set[Vertex] getStatesRegions(StateMachine sm):
 //ToDo: FinalState is State ?!?
 ((Set[Vertex])sm.eRootContainer.eAllContents.select(e|e.metaType == State || e.metaType == FinalState || e.metaType == Region))
;

// Get all States of the state machine
Set[Vertex] getStates(StateMachine sm):
 //ToDo: FinalState is State ?!?
 ((Set[Vertex])sm.eRootContainer.eAllContents.select(e|e.metaType == State || e.metaType == FinalState))
;

// Get all history states of the sate machine (these are Pseudostates of subtype DH or SH)
Set[Pseudostate] getHistoryStates(StateMachine sm):
 ((Set[Vertex])sm.eRootContainer.eAllContents.select(e|e.metaType == Pseudostate && ((((Vertex)e).isDH())||((Vertex)e).isSH())))
;

// Get all choice, join and fork states of the sate machine 
Set[Pseudostate] getRelevantPseudotates(StateMachine sm):
 ((Set[Vertex])sm.eRootContainer.eAllContents.select(e|e.metaType == Pseudostate && ((((Vertex)e).isChoice()||((Vertex)e).isJoin()||((Vertex)e).isFork()))))
;



// get all regions of the state machine
Set[Region] getRegions(StateMachine sm):
 ((Set[Region])sm.eAllContents.select(e|e.metaType == Region))
;

//-----------------------------------------------------------------------------

// Get all initial transitions of a state machine
Set[Transition] getInitialTransitions(StateMachine sm):
  let transitions = sm.getTransitions().select(e|(e.source).metaType == Pseudostate) :
  transitions.select(e|(e.source).isInitial())
;

// Get all deep history transitions of a state machine
Set[Transition] getDHDTransitions(StateMachine sm):
  let transitions = sm.getTransitions().select(e|(e.source).metaType == Pseudostate) :
  transitions.select(e|(e.source).isDH())
;

// Get all shallow history transitions of a state machine
Set[Transition] getSHDTransitions(StateMachine sm):
  let transitions = sm.getTransitions().select(e|(e.source).metaType == Pseudostate) :
  transitions.select(e|(e.source).isSH())
;

//-----------------------------------------------------------------------------

// Get qualified maude states for template
Set[String] getMaudeStates(StateMachine sm):
 ((Set[State])sm.getStates()).fullyHelp()
;

// Get qualified maude regions for template
Set[String] getMaudeRegions(StateMachine sm):
 ((Set[Region])sm.getRegions()).fullyHelp()
;

// Get qualified history maude states for template
Set[String] getHistoryMaudeStates(StateMachine sm):
 ((Set[Pseudostate])sm.getHistoryStates()).fullyHistoryHelp()
;

// Get qualified history maude states for template
Set[String] getPseudoMaudeStates(StateMachine sm):
 ((Set[Pseudostate])sm.getRelevantPseudotates()).fullyPseudoHelp()
;


//-----------------------------------------------------------------------------

// INTERNAL: Helpermethod to get the fully qualified name of a region
String fullyHelp(Region n):
 //'op R'+n.getId()+' : -> Region .\n'+
 '    eq '+n.getId()+' = '+ n.fullyQualifiedName()
;

// INTERNAL: Helpermethod to get the fully qualified name of a state
String fullyHelp(State n):
 //'op C'+n.getId()+' : -> ModState .\n'+
 '    eq '+n.getId()+' = '+ n.fullyQualifiedName()
;

// INTERNAL: Helpermethod to get the fully qualified name of a history pseudostate
String fullyHistoryHelp(Pseudostate n):
 //'op C'+n.getId()+' : -> ModState .\n'+
  if (n.isDH()) then
 '    eq '+n.getId()+' = '+ ((Region)n.getParent()).fullyQualifiedName() + ' : H*'
 else
 '    eq '+n.getId()+' = '+ ((Region)n.getParent()).fullyQualifiedName() + ' : H'
;


// INTERNAL: Helpermethod to get the fully qualified name of a pseudostate
String fullyPseudoHelp(Pseudostate n):
 //'op C'+n.getId()+' : -> ModState .\n'+
  if (n.isChoice()) then
 '    eq '+n.getId()+' = '+ ((Region)n.getParent()).fullyQualifiedName() + ' : choice "'+n.getId()+'"'
  else if (n.isFork()) then
 '    eq '+n.getId()+' = '+ ((Region)n.getParent()).fullyQualifiedName() + ' : fork "'+n.getId()+'"'
  else if (n.isJoin()) then
 '    eq '+n.getId()+' = '+ ((Region)n.getParent()).fullyQualifiedName() + ' : join "'+n.getId()+'"'
;


// INTERNAL: Helpermethod to get the fully qualified name of a region
String fullyQualifiedName(Region n) : n.getParent().metaType == StateMachine ? 'root(R "'+n.getId()+'")' :
   fullyQualifiedName(n.getParent())+' : R "'+n.getId()+'"'
;

// INTERNAL: Helpermethod to get the fully qualified name of a state
String fullyQualifiedName(State n) : n.getParent() == null ? n.getId() :
   fullyQualifiedName(n.getParent())+' : C "'+n.getId()+'"'
;

//-----------------------------------------------------------------------------

// INTERNAL: Helper method toe get the parent state of a region
State getParent(Region region):
 (State)getElementParent((Element)region)
;

// INTERNAL: Helper method toe get the parent state of a region
State getParent(Vertex vertex):
 (Region)getElementParent((Element)vertex)
;

// INTERNAL: Helper method toe get the parent region of a state
Region getParent(State state):
 (Region)getElementParent((Element)state)
;

// INTERNAL: Helper method toe get the parent region of a pseudo state
Region getParent(Pseudostate state):
 (Region)getElementParent((Element)state)
;

// INTERNAL: Helper method toe get the parent region of a pseudo state
Region getParent(Transition transition):
 (Region)getElementParent((Element)transition)
;

//-----------------------------------------------------------------------------

// Returns the model element (calling eContainer)
Element getElementParent(Element me) :
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getParent(org.eclipse.uml2.uml.Element)  
;


//-----------------------------------------------------------------------------

// Base helpermethod for generation basic (i.e. not involving pseudostates) 
// compound transitions  
//  
// Transitions are tuples of:
// source guard Action target 
String genMaudeBasicCompound(Transition n):
 '   eq ctr'+n.getId()+ ' = simpleTrans "ctr'+n.getId()+'" '+
 // Triggerevent
 n.trigger2() + ' ' +
// transition
 'tr'+n.getId()+' '+
// containingRegion
 ((Region)n.getParent()).getId() + ' .'   
;


//-----------------------------------------------------------------------------

// Base helpermethod for generation simple transitions, compound transitions
// are generated NOT using this method! 
//  
// Transitions are tuples of:
// source guard Action target 
String genMaude2(Transition n):
 '   eq tr'+n.getId()+ ' = basicTrans "tr'+n.getId()+'"'+
// source 
 '('+(n.source.getId()) + ') ' +
// 'empty '+ 
// guard 
 n.guard2() + ' ' +
// Action
 n.action2() + ' '+
// target 
 '('+n.target.getId()+') ' +' .'   
;


//-----------------------------------------------------------------------------

// Base helpermethod for generation normal (simple) transitions, complex one
// that include join/fork nodes as targets are generated NOT using this method! 
//  
// Transitions are tuples of:
// sources Triggerevent guard Action ListofForkActions ListofJoinActions targets containingRegion
String genMaude(Transition n):
 // ToDo: generate source and target sets
 //(n.source).toString() + '   ' + (n.target).toString() + '   ' + n.trigger2() + '   ' + n.action2()+ '   ' +((Region)n.container).getId()
 '   eq tr'+n.getId()+ ' = "tr'+n.getId()+'"'+
// sources (States only)
 '('+(n.source.getId()) + ') ' +
// 'empty '+ 
// Triggerevent
 n.trigger2() + ' ' +
// guard 
 n.guard2() + ' ' +
// Action
 n.action2() + 
// ListofForkActions ListofJoinActions
 ' skip skip ' +
// targets (States and history pseudostates only)
 '('+n.target.getId()+') ' +
// containingRegion
 ((Region)n.getParent()).getId() + ' .'   
;

//-----------------------------------------------------------------------------

// Helper method for generating transition definitions
String genDefault(Transition tr):
  let region = ((Pseudostate) tr.source).getParent() :
  ' eq def'+region.getId()+ ' = ' + region.getId() + ' default ' + ((State)tr.target).getId()+ ' .\n'  
;

// Helper method for generating transition definitions
String genDHDefault(Transition tr):
  let region = ((Pseudostate) tr.source).getParent() :
  ' eq dDef'+region.getId()+ ' = ' + region.getId() + ' deepdefault ' + ((State)tr.target).getId()+ ' .\n'  
;

// Helper method for generating transition definitions
String genSHDefault(Transition tr):
  let region = ((Pseudostate) tr.source).getParent() :
  ' eq sDef'+region.getId()+ ' = ' + region.getId() + ' shallowdefault ' + ((State)tr.target).getId()+ ' .\n'  
;

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

// Get all incoming transitions of a vertex
List[Transition] getIncomingTransitions(Vertex vertex) :
   vertex.eRootContainer.eAllContents.select(e|e.metaType == Transition && ((Transition)e).target == vertex)
;

//-----------------------------------------------------------------------------

// Get all outgoing transitions of a vertex
List[Transition] getOutgoingTransitions(Vertex vertex) :
   vertex.eRootContainer.eAllContents.select(e|e.metaType == Transition && ((Transition)e).source == vertex)
;

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

// Checks whether a transition is eligible to be considered by the maude code
// generation or in the other case is already handled by the maude simulator
// implicitly. 
Boolean isConsidered(Transition transition):
  let source = transition.source:
  let target = transition.target:
  !(source.isInitial() || source.isDH() || source.isSH() || source.isFork() || source.isJoin() 
  || target.isFork() || target.isJoin() || source.isChoice() || target.isChoice())
;

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

// Checks whether a transition does not involve pseudostates,
// i.e., it is a basic compound transition 
Boolean isBasicCompound(Transition transition):
  let source = transition.source:
  let target = transition.target:
  !(source.isPseudo() || (target.isPseudo() && !(target.isDH() || target.isSH())) ) 
;

//-----------------------------------------------------------------------------

// Checks whether a transition should appear as a basicTrans.
// This is not the case for initial, deep/shallow history default transitions
Boolean isBasicTrans(Transition transition):
  let source = transition.source:
  let target = transition.target:
  !(source.isInitial() || source.isDH() || source.isSH()) 
;

//-----------------------------------------------------------------------------

// Clear all done marked vertices
Void clearDoneVertices():
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.clearDoneVertices()  
;

// Returns true iff the vertex (pseudostate fork or join) has already been
// checked and not needs to be visited again.
// It means that already exists a transition containing it.
boolean isDone(Vertex vertex, String id):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.isDone(org.eclipse.uml2.uml.Vertex, java.lang.String)  
;

// Mark a vertex (pseudostate fork or join) that has just been visited
boolean markDone(Vertex vertex, String id):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.markDone(org.eclipse.uml2.uml.Vertex, java.lang.String)  
;

//-----------------------------------------------------------------------------

// Set a new last root region iff the new region is on a higher hierarchy level
Void setLastRootRegion(Region region) :
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.setLastRootRegion(org.eclipse.uml2.uml.Region)  
;

// Reset the last root region
Void resetLastRootRegion() :
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.resetLastRootRegion()  
;

// Get the last root region
Region getLastRootRegion() :
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getLastRootRegion()  
;

//-----------------------------------------------------------------------------

// This returns all transitions for a join or fork pseudostate
// It will return null otherwise
// The method will also set the root region of that complex transition and will
// determine the trigger, guard and action of the complex transition.
List[Transition] getPseudoTargetTransitions(Pseudostate pseudostate) :
    let reset = resetLastRootRegion():
    let reset2 = resetComplexTransitionInformation():
    // get all transitions that have a fork or join as a target
    let allTransitionList = getAllPseudostateTransitions(pseudostate,"OUTGOING"):
    let selectedTransitionList = allTransitionList.select(e|e.source.metaType == Pseudostate
         && ( e.source.isJoin() || e.source.isFork() || e.source.isChoice() )) :
    // Add information about complex transition for all sub-transitions
    allTransitionList.trigger2().addComplexTransitionInformation("TRIGGER") ->
    allTransitionList.action2().addComplexTransitionInformation("ACTION") ->
//    allTransitionList.trigger2().addComplexTransitionInformation("GUARD") ->
    // Return selected transitions
    selectedTransitionList
;

//-----------------------------------------------------------------------------

// This returns all transitions for a join or fork pseudostate
// It will return null otherwise
// The method will also set the root region of that complex transition and will
// determine the trigger, guard and action of the complex transition.
List[Transition] getPseudoSourceTransitions(Pseudostate pseudostate) :
    let reset = resetLastRootRegion():
    let reset2 = resetComplexTransitionInformation():
    // get all transitions that have a fork or join as a target
    let allTransitionList = getAllPseudostateTransitions(pseudostate,"INCOMING") :
    let selectedTransitionList = allTransitionList.select(e|e.target.metaType == Pseudostate
         && ( e.target.isJoin() || e.target.isFork() || e.target.isChoice() )) :
    // Add information about complex transition for all sub-transitions
    allTransitionList.trigger2().addComplexTransitionInformation("TRIGGER") ->
    allTransitionList.action2().addComplexTransitionInformation("ACTION") ->
//    allTransitionList.trigger2().addComplexTransitionInformation("GUARD") ->
    // Return selected transitions
    selectedTransitionList
;

//-----------------------------------------------------------------------------

// NOT NEEDED ANY MORE FOR *NEW* COMPLEX TRANSITION SYNTAX

// This returns all nodes for a join or fork pseudostate
// It will return null otherwise
// The method will also set the root region of that complex transition and will
// determine the trigger, guard and action of the complex transition.
Set[Vertex] getPseudoTargetVertices(Pseudostate pseudostate) :
    let reset = resetLastRootRegion():
    let reset2 = resetComplexTransitionInformation():
    // get all transitions that have a fork or join as a target
    let allTransitionList = getAllPseudostateTransitions(pseudostate,"OUTGOING"):
    let selectedTransitionList = allTransitionList.select(e|e.source.metaType == Pseudostate
         && ( e.source.isJoin() || e.source.isFork() )) :
    // Add information about complex transition for all sub-transitions
    allTransitionList.trigger2().addComplexTransitionInformation("TRIGGER") ->
    allTransitionList.action2().addComplexTransitionInformation("ACTION") ->
//    allTransitionList.trigger2().addComplexTransitionInformation("GUARD") ->
    // Return all target vertices
    selectedTransitionList.target
;

//-----------------------------------------------------------------------------

// NOT NEEDED ANY MORE FOR *NEW* COMPLEX TRANSITION SYNTAX

// This returns all nodes for a join or fork pseudostate
// It will return null otherwise
// The method will also set the root region of that complex transition and will
// determine the trigger, guard and action of the complex transition.
Set[Vertex] getPseudoSourceVertices(Pseudostate pseudostate) :
    let reset = resetLastRootRegion():
    let reset2 = resetComplexTransitionInformation():
    // get all transitions that have a fork or join as a target
    let allTransitionList = getAllPseudostateTransitions(pseudostate,"INCOMING") :
    let selectedTransitionList = allTransitionList.select(e|e.target.metaType == Pseudostate
         && ( e.target.isJoin() || e.target.isFork() )) :
    // Add information about complex transition for all sub-transitions
    allTransitionList.trigger2().addComplexTransitionInformation("TRIGGER") ->
    allTransitionList.action2().addComplexTransitionInformation("ACTION") ->
//    allTransitionList.trigger2().addComplexTransitionInformation("GUARD") ->
    // Return all source vertices
    selectedTransitionList.source
;

//-----------------------------------------------------------------------------

// INTERNAL: This will calculate all transitions for a complex transition that
// are either INCOMING or OUTGOING. These will be marked to be visited for
// one of both ids. The search (including backtracking) is exactly the same.
Set[Transition] getAllPseudostateTransitions(Pseudostate pseudostate, String id) :
   let incomingList = (List[Transition])pseudostate.getIncomingTransitions():
   let outgoingList = (List[Transition])pseudostate.getOutgoingTransitions():
   let incomingOutgoingList = {}:
   incomingOutgoingList.addAll(incomingList) ->
   incomingOutgoingList.addAll(outgoingList) ->
  if (markDone(pseudostate, id)) then (
  	 // Set the last root region of this pseudostate, if it is hierarchically higher
  	 // than it will be saved, otherwise it will be ignored
     setLastRootRegion(((Region)pseudostate.getParent())) ->
     getAllPseudostateTransitions2(incomingOutgoingList, id)
  ) else
     {}
;

// INTERNAL: This helper method will add the firstTransition to a list and return this in the
// case that the transition target is neither a fork or join node,
// otherwise it will recursively call and add transitions for that fork/join node.
Set[Transition] helperTarget(Vertex firstTransitionTarget, Transition firstTransition, String id) :
  let outList = {}:
  // add outgoing transitions to 'normal vertices'
  if (!firstTransitionTarget.isFork() && !firstTransitionTarget.isJoin()) then
      outList.add(firstTransition)
  else
     outList.addAll(getAllPseudostateTransitions((Pseudostate)firstTransitionTarget, id))
  ->
  outList
;

// INTERNAL: This helper method will add the firstTransition to a list and return this in the
// case that the transition source is neither a fork or join node,
// otherwise it will recursively call and add transitions for that fork/join node.
Set[Transition] helperSource(Vertex firstTransitionSource, Transition firstTransition, String id) :
  let outList = {}:
  // add incoming transitions from 'normal vertices'
  if (!firstTransitionSource.isFork() && !firstTransitionSource.isJoin()) then
      outList.add(firstTransition)
  else 
     outList.addAll(getAllPseudostateTransitions((Pseudostate)firstTransitionSource, id))
  ->
  outList
;

// INTERNAL: This helper method will add all transitions eigther incoming or outgoing ones
// that belong to the complex transition. This may result in subsequent recursive calls.
Set[Transition] getAllPseudostateTransitions2(List[Transition] transitionList, String id) :
  let firstTransition = transitionList.first():
  let firstTransitionSource = firstTransition.source:
  let firstTransitionTarget = firstTransition.target:
  let outList = {}:
  outList.addAll(helperTarget(firstTransitionTarget, firstTransition, id)) ->
  outList.addAll(helperSource(firstTransitionSource, firstTransition, id)) ->
  // add possibly other transitions
  if (transitionList.withoutFirst().size > 0) then
     outList.addAll(getAllPseudostateTransitions2(transitionList.withoutFirst(), id))
  else 
     {}
  ->
  outList
;

//-----------------------------------------------------------------------------

// Get the alias of a vertex
String getAlias(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getAlias(org.eclipse.uml2.uml.Vertex)
;

// Get the class identifier of a vertex
String getClassIdentifier(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getClassIdentifier(org.eclipse.uml2.uml.Vertex)
;

// Get the alias of a transition
String getAlias(Transition transition):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getAlias(org.eclipse.uml2.uml.Transition)
;

// Test whether a vertex is of type Initial
Boolean isInitial(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.isInitial(org.eclipse.uml2.uml.Vertex)
;

// Test whether a vertex is of type Deep History
Boolean isDH(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.isDH(org.eclipse.uml2.uml.Vertex)
;

// Test whether a vertex is of type Shallow History
Boolean isSH(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.isSH(org.eclipse.uml2.uml.Vertex)
;

// Test whether a vertex is of type Fork
Boolean isFork(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.isFork(org.eclipse.uml2.uml.Vertex)
;

// Test whether a vertex is of type Join
Boolean isJoin(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.isJoin(org.eclipse.uml2.uml.Vertex)
;

// Test whether a vertex is of type Choice
Boolean isChoice(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.isChoice(org.eclipse.uml2.uml.Vertex)
;

// Test whether a vertex is a pseudostate
Boolean isPseudo(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.isPseudostate(org.eclipse.uml2.uml.Vertex)
;



//-----------------------------------------------------------------------------

// Get the ID of a region
String getId(Region region):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getId(org.eclipse.uml2.uml.Region)
;

// Get the ID of a vertex
String getId(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getId(org.eclipse.uml2.uml.Vertex)
;

// Get the EMF ID of a vertex
String getEMFId(Vertex vertex):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getEMFId(org.eclipse.uml2.uml.Vertex)
;

// Get the EMF ID of a region
String getEMFId(Region region):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getEMFId(org.eclipse.uml2.uml.Region)
;

// Get the ID of a transition
String getId(Transition transition):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getId(org.eclipse.uml2.uml.Transition)
;

// Get the hash value of a String
String hash(String string):
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.hash(java.lang.String)
;

//-----------------------------------------------------------------------------

// Do not print out a string, this can help for an Xpand template when whe 
// want to call a method but don't want to write out the resulting return values.
Void ignore(String string) :
  null  
;

//-----------------------------------------------------------------------------

// Add a Pseudostate to a temporary list
Void addPseudostate(Pseudostate pseudostate) :
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.addPseudostate(org.eclipse.uml2.uml.Pseudostate)
;
	
// Get the saved Pseudostates
List[Pseudostate] getPseudostates() :
  JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getPseudostates()
;
	
// Reset the Pseudostates
Void resetPseudostates() :
 JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.resetPseudostates()
;	

//-----------------------------------------------------------------------------

// Add information about a complex transition (id == TRIGGER, GUARD or ACTION)
Void addComplexTransitionInformation(String item, String id) :
 JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.addComplexTransitionInformation(java.lang.String, java.lang.String)
;

// Get information about a complex transition (id == TRIGGER, GUARD or ACTION)
List[String] getComplexTransitionInformation(String id) :
 JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.getComplexTransitionInformation(java.lang.String)
;

// Reset information about a complex tranistion
Void resetComplexTransitionInformation() :
 JAVA de.cau.cs.kieler.uml2.sim.JavaEscape.resetComplexTransitionInformation()
;
