module de.cau.cs.kieler.annotations.Annotations

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

// Project information
var rootPath = ".."
var projectName = "de.cau.cs.kieler.annotations"
var grammar = "de.cau.cs.kieler.annotations.Annotations"
var grammarURI = "classpath:/de/cau/cs/kieler/annotations/Annotations.xtext"
// File extension
var file.extensions = "anno"
// Parser backtracking
var backtrack = false
// Indicates languages that that contained in the same plugin as another language but does not influence major package naming.
// This will adjust package names.
// I.e.: KExt in KEpressions
var secondaryGrammar = false
// Cleans the src-gen folder (disable for secondary grammars)
var clean = true

Workflow {
    bean = StandaloneSetup {
        platformUri = "${rootPath}"
        registerGeneratedEPackage = "de.cau.cs.kieler.annotations.AnnotationsPackage"
        registerGenModelFile = "platform:/resource/de.cau.cs.kieler.annotations/model/annotations.genmodel"
    }
    
    component = XtextGenerator {
        configuration = de.cau.cs.kieler.annotations.xtext.KielerGeneratorModule {
            project = de.cau.cs.kieler.annotations.xtext.KielerProjectConfig {
                projectRelatedPackageNames = secondaryGrammar
                grammar = grammar
                baseName = projectName
                rootPath = rootPath
                eclipsePlugin = {
                    enabled = true
                }
                createEclipseMetaData = true
                genericIde = {
                    enabled = true
                } 
            }
            
            code = {
                encoding = "UTF-8"
                preferXtendStubs = true
            }
        }
        
        cleaner = {
            enabled = clean
        }
               
        language = XtextGeneratorLanguage {
            name = grammar
            fileExtensions = file.extensions
            grammarUri = grammarURI
            
            fragment = grammarAccess.GrammarAccessFragment2 {}
            // ONLY uncomment when EMF model should be generated from grammar
            // fragment = ecore.EMFGeneratorFragment2 {}
            fragment = serializer.SerializerFragment2 {}
            fragment = resourceFactory.ResourceFactoryFragment2 {}
            fragment = parser.antlr.XtextAntlrGeneratorFragment2 {
                options = {
                    backtrack = backtrack
                    classSplitting = true
                }
            }
            fragment = validation.ValidatorFragment2 {}
            fragment = scoping.ImportNamespacesScopingFragment2 {}
            fragment = exporting.QualifiedNamesFragment2 {}
            fragment = formatting.Formatter2Fragment2 {}
            fragment = ui.labeling.LabelProviderFragment2 {}
            fragment = ui.outline.QuickOutlineFragment2 {}
            fragment = ui.outline.OutlineTreeProviderFragment2 {}
            fragment = ui.quickfix.QuickfixProviderFragment2 {}
            fragment = ui.contentAssist.ContentAssistFragment2 {}
            fragment = ui.refactoring.RefactorElementNameFragment2 {}
            fragment = xtext.MonacoHighlightingFragment {}
   
        }
    }
}
