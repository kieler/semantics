/*
 * generated by Xtext
 */
package de.cau.cs.kieler.sccharts.text.scoping

import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.EReference
import de.cau.cs.kieler.sccharts.Transition
import org.eclipse.xtext.scoping.IScope
import com.google.inject.Inject
import de.cau.cs.kieler.sccharts.State
import de.cau.cs.kieler.sccharts.ControlflowRegion
import de.cau.cs.kieler.kexpressions.KExpressionsPackage
import de.cau.cs.kieler.kexpressions.ReferenceDeclaration
import de.cau.cs.kieler.sccharts.Scope
import de.cau.cs.kieler.annotations.extensions.AnnotationsExtensions
import de.cau.cs.kieler.sccharts.extensions.SCChartsCoreExtensions
import de.cau.cs.kieler.sccharts.ScopeCall
import de.cau.cs.kieler.kexpressions.ValuedObject
import de.cau.cs.kieler.kexpressions.extensions.KExpressionsDeclarationExtensions
import de.cau.cs.kieler.kexpressions.Parameter

/**
 * This class contains custom scoping description.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#scoping
 * on how and when to use it.
 *
 */
class SCTXScopeProvider extends de.cau.cs.kieler.kexpressions.kext.scoping.KExtScopeProvider {
    
    @Inject extension SCChartsCoreExtensions
    @Inject extension AnnotationsExtensions
    @Inject extension KExpressionsDeclarationExtensions
    
    @Inject SCTXQualifiedNameProvider nameProvider

    override getScope(EObject context, EReference reference) {
//        println(context + "\n  " + reference)
        
        switch(context) {
            Transition: return getScopeForTransition(context, reference)
            ScopeCall: return getScopeForScopeCall(context, reference)  
        }
        
        return super.getScope(context, reference);
    }
    
    protected def IScope getScopeForTransition(Transition transition, EReference reference) {
        val states = <State> newArrayList
        val parentState = transition.eContainer as State
        val parentRegion = parentState.eContainer as ControlflowRegion
        
        parentRegion.states.forEach[ 
            states += it 
        ]
        
        return SCTScopes.scopeFor(states)
    }
    
    protected def IScope getScopeForScopeCall(ScopeCall scopeCall, EReference reference) {
        if (reference.name.equals("scope")) {
            return super.getScope(scopeCall as EObject, reference)
        }
        
        return super.getScope(scopeCall as EObject, reference)
    }
        
    override def IScope getScopeForParameter(Parameter parameter, EReference reference) {        
        if (reference.name.equals("explicitBinding")) {
            val voCandidates = <ValuedObject> newArrayList
            
            val scopeCall = parameter.eContainer as ScopeCall
            if (scopeCall != null && scopeCall.scope != null) {
                for (declaration : scopeCall.scope.variableDeclarations.filter[ input || output]) {
                    voCandidates += declaration.valuedObjects
                }
            }
            
            return SCTScopes.scopeFor(voCandidates)
        }
        
        return super.getScopeForParameter(parameter, reference)
    }
    
    override def IScope getScopeForReferenceDeclaration(EObject context, EReference reference) {
        if (reference == KExpressionsPackage.Literals.REFERENCE_DECLARATION__REFERENCE) {
            val candidates = <Scope> newArrayList 
            
            val declaration = context
            if (declaration instanceof ReferenceDeclaration) {
                val superScope = super.getScope(context.eContainer, reference)
                val root = context.root.asSCCharts
                candidates += root.rootStates
                val imports = root.getStringPragmas("import").toList
                
                val res = context.eResource      
                if (res != null) {
                    val resSet = res.resourceSet
                    
                    if (resSet != null) {
                        val resSetFilter = resSet.resources.filter[ 
                            val uri = it.URI.toPlatformString(true)
//                            println(uri)
                            return imports.exists[ uri.endsWith(it.values.head) ]
                        ]
                        for(r : resSetFilter) {
                            candidates += r.contents.filter[ it instanceof State ].map[ it as Scope ]
                        }
                    }
                }      
                
                return SCTScopes.scopeFor(candidates, nameProvider, superScope)
            }
        }
        return context.getScopeHierarchical(reference)
    }       

}
