module de.cau.cs.kieler.sccharts.text.SCTX

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.ui.generator.*

var grammarURI = "classpath:/de/cau/cs/kieler/sccharts/text/SCTX.xtext"
var file.extensions = "sctx"
var projectName = "de.cau.cs.kieler.sccharts.text"
var runtimeProject = "../${projectName}"

Workflow {
	bean = StandaloneSetup {
		platformUri = "${runtimeProject}/.."
		registerGeneratedEPackage = "de.cau.cs.kieler.annotations.AnnotationsPackage"		
		registerGeneratedEPackage = "de.cau.cs.kieler.kexpressions.KExpressionsPackage"		
		registerGeneratedEPackage = "de.cau.cs.kieler.kexpressions.keffects.KEffectsPackage"
		registerGeneratedEPackage = "de.cau.cs.kieler.kexpressions.kext.KExtPackage"
		registerGeneratedEPackage = "de.cau.cs.kieler.sccharts.SCChartsPackage"
		registerGenModelFile = "platform:/resource/de.cau.cs.kieler.annotations/model/annotations.genmodel"
		registerGenModelFile = "platform:/resource/de.cau.cs.kieler.kexpressions/model/kexpressions.genmodel"
		registerGenModelFile = "platform:/resource/de.cau.cs.kieler.kexpressions/model/keffects.genmodel"
		registerGenModelFile = "platform:/resource/de.cau.cs.kieler.kexpressions/model/kext.genmodel"
		registerGenModelFile = "platform:/resource/de.cau.cs.kieler.sccharts/model/sccharts.genmodel"
	}
	
	component = DirectoryCleaner {
		directory = "${runtimeProject}/src-gen/de/cau/cs/kieler/sccharts/text"
	}
	
	component = DirectoryCleaner {
		directory = "${runtimeProject}.ui/src-gen/de/cau/cs/kieler/sccharts/text"
	}
	
	component = Generator {
		pathRtProject = runtimeProject
		pathUiProject = "${runtimeProject}.ui"
		projectNameRt = projectName
		projectNameUi = "${projectName}.ui"
		
		language = {
			uri = grammarURI
			fileExtensions = file.extensions
			
			// Java API to access grammar elements (required by several other fragments)
			fragment = grammarAccess.GrammarAccessFragment {}
			
			// generates Java API for the generated EPackages
			fragment = ecore.EMFGeneratorFragment {} //referencedGenModels = "platform:/resource/de.cau.cs.kieler.sccharts/model/sccharts.genmodel" }
			
			// the serialization component
			fragment = serializer.SerializerFragment {
			    generateXtendStub = true
			}
			
			// a custom ResourceFactory for use with EMF 
			fragment = resourceFactory.ResourceFactoryFragment {
				fileExtensions = file.extensions
			}
				
			// the Antlr parser
			fragment = org.eclipse.xtext.generator.parser.antlr.XtextAntlrGeneratorFragment {
				options = {
					classSplitting = true
					backtrack = true
				}
			}

			// java-based API for validation 
			fragment = validation.JavaValidatorFragment {
            }
            
			// scoping and exporting API 
			fragment = scoping.ImportNamespacesScopingFragment {
//			 	generateStub = false
			 	generateXtendStub = true
			}
			fragment = exporting.QualifiedNamesFragment {}


			// formatter API 
            fragment = formatting2.Formatter2Fragment auto-inject {}

			// labeling API 
			fragment = labeling.LabelProviderFragment {}

			// outline API 
			fragment = outline.OutlineTreeProviderFragment {}
			fragment = outline.QuickOutlineFragment {}

			// java-based API for content assistance 
			fragment = contentAssist.JavaBasedContentAssistFragment {}
			
			// antlr parser generator tailored for content assist 
			fragment = org.eclipse.xtext.generator.parser.antlr.XtextAntlrUiGeneratorFragment { 
				options = {
					classSplitting = true
					backtrack = true
				}
			}

            // This fragment introduces the registration of the NatureAddingCallback. 
			fragment = builder.BuilderIntegrationFragment {}

			// project wizard (optional) 
			
			// fragment = projectWizard.SimpleProjectWizardFragment {
			//	generatorProjectName = "${projectName}.generator" 
			//	modelFileExtension = file.extensions
			// }
			
			// quickfix API 
			fragment = quickfix.QuickfixProviderFragment {}
			
			fragment = refactoring.RefactorElementNameFragment {}
		}
	}
}